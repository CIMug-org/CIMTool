<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:xs="http://www.w3.org/2001/XMLSchema"
	xmlns:a="http://langdale.com.au/2005/Message#"
	xmlns:sawsdl="http://www.w3.org/ns/sawsdl">

	<xsl:output indent="yes" method="text" encoding="utf-8" />
	<xsl:param name="version"></xsl:param>
	<xsl:param name="baseURI"></xsl:param>
	<xsl:param name="envelope">Profile</xsl:param>

	<xsl:template match="a:Catalog">
		<!--  the top level template  -->
		-- Schema for <xsl:value-of select="$envelope" />
		-- Generated by CIMTool <xsl:value-of select="$version" /> http://cimtool.org

		<xsl:apply-templates/>
	</xsl:template>

	<xsl:template match="a:ComplexType|a:Root">
		<!-- a table -->
		<xsl:call-template name="annotate" />
		CREATE TABLE "<xsl:value-of select="@name"/>" (
			mrid NUMBER NOT NULL UNIQUE
			<xsl:apply-templates/>
		);
	</xsl:template>

	<xsl:template match="a:EnumeratedType">
		<!-- a reference table for an enumeration -->
		<xsl:call-template name="annotate" />
		CREATE TABLE "<xsl:value-of select="@name"/>" ( name CHAR VARYING(30) );
		<xsl:variable name="name" select="@name"/>
		<xsl:for-each select="a:EnumeratedValue">
			<!-- inserts one value into a reference table -->
			<xsl:call-template name="annotate" />
			INSERT INTO "<xsl:value-of select="$name"/>" ( name ) 
			VALUES ( '<xsl:value-of select="@value"/>' )  
		</xsl:for-each>
	</xsl:template>
	
	<xsl:template match="a:SuperType">
		<!-- a primary key constraint on a subclass table -->
	</xsl:template>

	<xsl:template match="a:Instance|a:Reference">
		<xsl:if test="@maxOccurs &lt;= 1 and @name != 'mrid'">
			<!-- a foreign key column -->
			<xsl:call-template name="annotate" />,
			"<xsl:value-of select="@name"/>" NUMBER
			<xsl:if test="@minOccurs > 0">NOT NULL</xsl:if>
			REFERENCES "<xsl:value-of select="@type"/>" ( mrid ) 
		</xsl:if>
	</xsl:template>
	
	<xsl:template match="a:Enumerated">
		<!-- a foreign key column for a reference table -->
		<xsl:call-template name="annotate" />,
		"<xsl:value-of select="@name"/>" CHAR VARYING(30)
		<xsl:if test="@minOccurs > 0">NOT NULL</xsl:if>
		REFERENCES "<xsl:value-of select="@type"/>" ( name ) 
	</xsl:template>

	<xsl:template match="a:Simple|a:Domain">
		<!-- a simple column  -->
		<xsl:call-template name="annotate" />
		, "<xsl:value-of select="@name"/>" 
		<xsl:choose>
			<xsl:when test="@xstype = 'string'">CHAR VARYING(30)</xsl:when>
			<xsl:when test="@xstype = 'integer' or @xstype = 'int'">INTEGER</xsl:when>
			<xsl:when test="@xstype = 'float'">FLOAT</xsl:when>
			<xsl:otherwise>CHAR VARYING(30)</xsl:otherwise>
		</xsl:choose>
		<xsl:if test="@minOccurs > 0">NOT NULL</xsl:if>
	</xsl:template>

	<xsl:template match="text()">
		<!--  dont pass text through  -->
	</xsl:template>

	<xsl:template name="annotate">
		<!--  generate and annotation -->
		<xsl:apply-templates mode="annotate"/>
	</xsl:template>

	<xsl:template match="a:Comment|a:Note" mode="annotate">
		<!--  generate human readable annotation -->
	</xsl:template>

	<xsl:template match="node()" mode="annotate">
		<!-- dont pass any defaults in annotate mode -->
	</xsl:template>

</xsl:stylesheet>